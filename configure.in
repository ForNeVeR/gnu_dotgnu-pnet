dnl Process this file with autoconf to produce a configure script.
AC_INIT(runtime/System/Object.cs)
AM_INIT_AUTOMAKE(pnetlib, 0.6.1)
AM_MAINTAINER_MODE

dnl Extra substitution variables.
AC_SUBST(CSANT)
AC_SUBST(CSANT_FLAGS)
AC_SUBST(CSHARP_COMPILER)
AC_SUBST(CSHARP_COMPILER_CYGWIN)
AC_SUBST(CSHARP_PLUGIN)
AC_SUBST(CSCC_FLAGS)
AC_SUBST(RESGEN)
AC_SUBST(RESGEN_FLAGS)
AC_SUBST(RESGEN_LATIN1)
AC_SUBST(ILRUN)
AC_SUBST(ILFIND)
AC_SUBST(ASSEMBLY_LINKER)
AC_SUBST(PNET_PATH)
AC_SUBST(X11_LIB)
AC_SUBST(XFT_LIB)

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL
AC_PATH_PROG(CYGPATH, cygpath)
AC_PATH_PROG(TREECC, treecc,, ${prefix}/bin:$PATH)
if test -z "$TREECC" ; then
    echo "treecc is required to build and can be obtained from"
    echo "http://www.southern-storm.com.au/treecc.html"
    exit 1
fi

dnl Option to enable the Xft library for Freetype font support.
AC_ARG_ENABLE(xft,
[  --enable-xft            Enable Xft for Freetype font support],
[case "${enableval}" in
  yes) xft=true ;;
  no)  xft=false ;;
  *) AC_MSG_ERROR(bad value ${enableval} for --enable-xft) ;;
esac],[xft=false])

dnl Check for file extensions.
AC_EXEEXT
AC_OBJEXT

dnl Find X and related definitions, for building "libXsharpSupport.so".
AC_PATH_XTRA
AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_HEADERS(time.h sys/time.h sys/types.h sys/select.h unistd.h)
AC_CHECK_FUNCS(select)

dnl Determine if we should add -lX11 and -lXft to the link line
dnl of "libXsharpSupport.so".
if test "x$no_x" = "xyes" ; then
	X11_LIB=
	XFT_LIB=
else
	X11_LIB=-lX11
	if test x$xft = xtrue ; then
		XFT_LIB=-lXft
		AC_DEFINE(USE_XFT_EXTENSION)
	else
		XFT_LIB=
	fi
fi

dnl Determine if we should add -lXft to the link line of "libXsharpSupport.so".

dnl The --with-pnet option can be used to specify the location of
dnl Portable.NET, so that we know where to get the C# compiler, etc.
dnl The default location is assumed to be "../pnet".
AC_ARG_WITH(pnet,
[  --with-pnet=DIR         specify the location of Portable.NET],
[
	if test -n "$withval"; then
		case "$withval" in
		  ../*) PNET_PATH="`pwd`/$withval" ;;
		     *) PNET_PATH="$withval" ;;
		esac
	else
		PNET_PATH="`pwd`/../pnet"
	fi
],
[
	PNET_PATH="`pwd`/../pnet"
])

dnl The --with-mstools option can be used to force the use of
dnl Microsoft's toolchain instead of Portable.NET's.
AC_ARG_WITH(mstools,
[  --with-mstools          use Microsoft tools instead of Portable.NET's],
[
	MSTOOLS=yes
],
[
	MSTOOLS=no
])

dnl The --with-profile=NAME option is used to change the build profile.
AC_SUBST(PROFILE_NAME)
AC_ARG_WITH(profile,
[  --with-profile=NAME     use build profile NAME],
[
	if test -f "${srcdir}/profiles/${withval}" ; then
		PROFILE_NAME="${withval}"
	else
		AC_MSG_ERROR(unknown build profile ${withval})
	fi
],
[
	PROFILE_NAME=full
])

dnl Find the Portable.NET "csant" program.
AC_PATH_PROG(CSANT, csant,, $PNET_PATH/csant:${prefix}/bin:$PATH)
if test "$CSANT" = "" ; then
	echo '**************************************************************'
	echo "Portable.NET's csant tool is required to build the system."
	echo "Use the --with-pnet option to specify Portable.NET's location."
	echo '**************************************************************'
	exit 1
fi

dnl Find the .NET resource generator to use.
if test "$MSTOOLS" = "no"; then
	AC_PATH_PROG(RESGEN, resgen,, $PNET_PATH/resgen:${prefix}/bin:$PATH)
	if test "$RESGEN" = "" ; then
		echo '**************************************************************'
		echo "Portable.NET's or Microsoft's resgen is required to build."
		echo "Use the --with-pnet option to specify Portable.NET's location."
		echo '**************************************************************'
		exit 1
	fi
else
	AC_PATH_PROG(RESGEN, resgen)
	if test "$RESGEN" = "" ; then
		echo '***********************************************************'
		echo "Could not locate Microsoft's resource utility (resgen.exe)."
		echo '***********************************************************'
		exit 1
	fi
fi

dnl Find the Microsoft "al" assembly linker, for satellite resources.
if test "$MSTOOLS" = "yes"; then
	AC_PATH_PROG(ASSEMBLY_LINKER, al)
else
	ASSEMBLY_LINKER=pnet
fi

dnl Find the Portable.NET "ilgac" program.
AC_PATH_PROG(ILGAC, ilgac,, $PNET_PATH/ilgac:${prefix}/bin:$PATH)
if test "$ILGAC" = "" ; then
	echo '**************************************************************'
	echo "Portable.NET's ilgac tool is required to build the system."
	echo "Use the --with-pnet option to specify Portable.NET's location."
	echo '**************************************************************'
	exit 1
fi

dnl Find the C# compiler to use.
if test "$MSTOOLS" = "no"; then
	AC_PATH_PROG(CSHARP_COMPILER, cscc,, $PNET_PATH/cscc:${prefix}/bin:$PATH)
	if test "$CSHARP_COMPILER" = "" ; then
		AC_PATH_PROG(CSHARP_COMPILER, csc)
		if test "$CSHARP_COMPILER" = ""; then
			echo '******************************************************'
			echo "Portable.NET's cscc or Microsoft's csc.exe is required"
			echo "to build.  Use the --with-pnet option to specify the"
			echo "location of the Portable.NET source tree."
			echo '******************************************************'
			exit 1
		fi
		CSANT_FLAGS='--compiler csc -Dcsc="$(CSHARP_COMPILER)" --profile "$(top_srcdir)/profiles/$(PROFILE_NAME)" -Dresgen="$(RESGEN)" -Dilgac="$(ILGAC)" --assembly-cache "$(libdir)/cscc/lib"'
		CSHARP_COMPILER_CYGWIN="$CSHARP_COMPILER"
	else
		AC_PATH_PROG(CSHARP_PLUGIN, cscc-cs,, $PNET_PATH/cscc:${prefix}/lib/cscc/plugins:${prefix}/bin:$PATH)
		if test "x$CYGPATH" = "x" ; then
			CSHARP_COMPILER_CYGWIN="$CSHARP_COMPILER"
		else
			# Convert the cscc and cscc-cs paths into their Windows
			# forms, to ensure that spawns from csant work correctly.
			CSHARP_COMPILER_CYGWIN="$CSHARP_COMPILER"
			CSHARP_COMPILER=`"$CYGPATH" -w "$CSHARP_COMPILER"`
			CSHARP_PLUGIN=`"$CYGPATH" -w "$CSHARP_PLUGIN"`
		fi
		CSANT_FLAGS='--compiler cscc -Dcscc="$(CSHARP_COMPILER)" -Dcscc.plugins.cs="$(CSHARP_PLUGIN)" --profile "$(top_srcdir)/profiles/$(PROFILE_NAME)" -Dresgen="$(RESGEN)" -Dilgac="$(ILGAC)" --assembly-cache "$(libdir)/cscc/lib"'
	fi
else
	AC_PATH_PROG(CSHARP_COMPILER, csc)
	if test "$CSHARP_COMPILER" = ""; then
		echo '***************************************************'
		echo "Could not locate Microsoft's C# compiler (csc.exe)."
		echo '***************************************************'
		exit 1
	fi
	CSANT_FLAGS='--compiler csc -Dcsc="$(CSHARP_COMPILER)" --profile "$(top_srcdir)/profiles/$(PROFILE_NAME)" -Dresgen="$(RESGEN)" -Dilgac="$(ILGAC)" --assembly-cache "$(libdir)/cscc/lib"'
	CSHARP_COMPILER_CYGWIN="$CSHARP_COMPILER"
fi

dnl Determine which version of RESGEN we are using (Portable.NET or Microsoft).
RESGEN_ERROR=`"$RESGEN" -v.txt 2>&1`
case "$RESGEN_ERROR" in
  RESGEN*) RESGEN_FLAGS="-tR"
  		   RESGEN_LATIN1="-l" ;;
        *) RESGEN_FLAGS="/compile"
		   RESGEN_LATIN1="" ;;
esac
AM_CONDITIONAL(PNET_RESGEN, test "$RESGEN_LATIN1" != "")

dnl Find the Portable.NET runtime engine.
AC_PATH_PROG(ILRUN, ilrun,, $PNET_PATH/engine:${prefix}/bin:$PATH)
if test "$ILRUN" = "" ; then
	echo '**************************************************************'
	echo "Portable.NET is required to run the test suite."
	echo "Use the --with-pnet option to specify Portable.NET's location."
	echo '**************************************************************'
	exit 1
fi

dnl Find the Portable.NET ilfind program.
AC_PATH_PROG(ILFIND, ilfind,, $PNET_PATH/ilfind:${prefix}/bin:$PATH)
if test "$ILFIND" = "" ; then
	echo '**************************************************************'
	echo "Portable.NET's ilfind is required to build the system."
	echo "Use the --with-pnet option to specify Portable.NET's location."
	echo '**************************************************************'
	exit 1
fi

dnl Determine if the system supports reflection, because there
dnl are some things that we can't build or test without reflection.
REF_FLAG=`grep 'CONFIG_REFLECTION" value="true"' "${srcdir}/profiles/${PROFILE_NAME}"`
AM_CONDITIONAL(HAS_REFLECTION, test "x$REF_FLAG" != "x")

dnl The --with-types option can be used to specify the sizes of
dnl the native int and long types for Xsharp.  Set to one of:
dnl
dnl		--with-types=il32
dnl			int and long are both 32-bit.
dnl		--with-types=il64
dnl			int and long are both 64-bit.
dnl		--with-types=i32l64
dnl			int is 32-bit, long is 64-bit.
dnl		--with-types=i64l32
dnl			int is 64-bit, long is 32-bit.
dnl
dnl If none of these are supplied, configure will auto-detect.
AC_ARG_WITH(types,
[  --with-types=SPEC       specify the location of Portable.NET],
[
	if test -n "$withval"; then
		case "$withval" in
		  il32|il64|i32l64|i64l32) TYPE_SPEC="${withval}" ;;
		  *) AC_MSG_ERROR([invalid type size specification ${withval}]) ;;
		esac
	else
		TYPE_SPEC=no
	fi
],
[
	TYPE_SPEC=no
])

dnl A macro that helps detect the size of types in a cross-compile environment.
AC_DEFUN([AC_COMPILE_CHECK_SIZEOF],
[changequote(<<, >>)dnl
dnl The name to #define.
define(<<AC_TYPE_NAME>>, translit(sizeof_$1, [a-z *], [A-Z_P]))dnl
dnl The cache variable name.
define(<<AC_CV_NAME>>, translit(ac_cv_sizeof_$1, [ *], [_p]))dnl
changequote([, ])dnl
AC_MSG_CHECKING(size of $1)
AC_CACHE_VAL(AC_CV_NAME,
[for ac_size in 4 8 1 2 12 16 $2 ; do # List sizes in rough order of prevalence.
  AC_TRY_COMPILE([#include "confdefs.h"
#include <sys/types.h>
], [switch (0) case 0: case (sizeof ($1) == $ac_size):;], AC_CV_NAME=$ac_size)
  if test x$AC_CV_NAME != x ; then break; fi
done
])
if test x$AC_CV_NAME = x ; then
  AC_CV_NAME=0
fi
AC_MSG_RESULT($AC_CV_NAME)
AC_DEFINE_UNQUOTED(AC_TYPE_NAME, $AC_CV_NAME, [The number of bytes in type $1])
undefine([AC_TYPE_NAME])dnl
undefine([AC_CV_NAME])dnl
])

dnl Determine the definitions of int, uint, long, and ulong on this platform.
AC_SUBST(X_int)
AC_SUBST(X_uint)
AC_SUBST(X_long)
AC_SUBST(X_ulong)
case "$TYPE_SPEC" in
	il32)	X_int=int
		  	X_uint=uint
		  	X_long=int
		  	X_ulong=uint ;;
	il64)	X_int=long
		  	X_uint=ulong
		  	X_long=long
		  	X_ulong=ulong ;;
	i32l64) X_int=int
			X_uint=uint
			X_long=long
			X_ulong=ulong ;;
	i64l32)	X_int=long
			X_uint=ulong
			X_long=int
			X_ulong=uint ;;
	*)	AC_COMPILE_CHECK_SIZEOF(int, 4)
		AC_COMPILE_CHECK_SIZEOF(long, 4)
		if test "$ac_cv_sizeof_int" = 4 ; then
			X_int=int
			X_uint=uint
		elif test "$ac_cv_sizeof_int" = 8 ; then
			X_int=long
			X_uint=ulong
		else
			AC_MSG_ERROR(int is neither 32-bits nor 64-bits on this platform)
		fi
		if test "$ac_cv_sizeof_long" = 4 ; then
			X_long=int
			X_ulong=uint
		elif test "$ac_cv_sizeof_long" = 8 ; then
			X_long=long
			X_ulong=ulong
		else
			AC_MSG_ERROR(long is neither 32-bits nor 64-bits on this platform)
		fi ;;
esac
AC_MSG_CHECKING(for the correct int mapping)
AC_MSG_RESULT($X_int)
AC_MSG_CHECKING(for the correct long mapping)
AC_MSG_RESULT($X_long)

AC_OUTPUT([
Makefile
tools/Makefile
runtime/Makefile
I18N/Makefile
DotGNU.SSL/Makefile
System/Makefile
System.Xml/Makefile
System.Net.IrDA/Makefile
csunit/Makefile
csunit/TestVersion.cs
csupport/Makefile
compat/Makefile
ISymWrapper/Makefile
SharpZipLib/Makefile
System.Configuration.Install/Makefile
JScript/Makefile
Basic/Makefile
DotGNU.Images/Makefile
Xsharp/Makefile
Xsharp/Xlib.cs
System.Drawing/Makefile
System.Drawing.Xsharp/Makefile
System.Drawing.Win32/Makefile
System.Drawing.Postscript/Makefile
System.EnterpriseServices/Makefile
System.Windows.Forms/Makefile
DotGNU.Terminal/Makefile
tests/Makefile
tests/System/Makefile
tests/System.Xml/Makefile
tests/runtime/Makefile
tests/JScript/Makefile
resources/Makefile
resources/en_US/Makefile
resources/en_US/runtime/Makefile
resources/en_US/I18N/Makefile
resources/en_US/System/Makefile
resources/en_US/System.Xml/Makefile
resources/en_US/System.Net.IrDA/Makefile
resources/en_US/Xsharp/Makefile
resources/en_US/System.Configuration.Install/Makefile
resources/en_US/System.Drawing/Makefile
resources/en_US/System.Windows.Forms/Makefile
resources/en_US/Basic/Makefile
resources/de_DE/Makefile
resources/de_DE/Basic/Makefile 
resources/de_DE/I18N/Makefile
resources/de_DE/System/Makefile
resources/de_DE/System.Configuration.Install/Makefile
resources/de_DE/System.Drawing/Makefile
resources/de_DE/System.Windows.Forms/Makefile
resources/de_DE/System.Xml/Makefile
resources/de_DE/System.Net.IrDA/Makefile
resources/de_DE/Xsharp/Makefile
resources/de_DE/runtime/Makefile
resources/ca_ES/Makefile
resources/ca_ES/Basic/Makefile 
resources/ca_ES/I18N/Makefile
resources/ca_ES/System/Makefile
resources/ca_ES/System.Configuration.Install/Makefile
resources/ca_ES/System.Drawing/Makefile
resources/ca_ES/System.Windows.Forms/Makefile
resources/ca_ES/System.Xml/Makefile
resources/ca_ES/System.Net.IrDA/Makefile
resources/ca_ES/Xsharp/Makefile
resources/ca_ES/runtime/Makefile
samples/Makefile
samples/ilrun.sh
tools/run_test.sh
tools/link_res.sh
pnetlib.spec])

chmod +x "${srcdir}/tools/link_res.sh"
chmod +x "${srcdir}/samples/ilrun.sh"
